/*
 * Licensed to Miguel Arregui ("marregui") under one or more contributor
 * license agreements. See the LICENSE file distributed with this work
 * for additional information regarding copyright ownership. You may
 * obtain a copy at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the
 * License for the specific language governing permissions and limitations
 * under the License.
 *
 * Copyright 2020, Miguel Arregui a.k.a. marregui
 */

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

application {
    mainClass = 'io.mygupsql.MyGupSql'
}

repositories {
    mavenCentral()
}

group = 'io.mygupsql'
version = '1.0.0-SNAPSHOT'
description = 'desktop user interface to common postgress ' +
        'wire protocol compatible databases.' +
        'sends SQL and renders results in a nice GUI.'

dependencies {
    implementation 'org.postgresql:postgresql:42.2.9'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-commons:1.7.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:3.5.10'
}

jar {
    manifest {
        attributes 'Main-Class': 'io.mygupsql.MyGupSql'
    }

    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    archiveClassifier.set('all')
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    } {
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
    with jar
}

artifacts {
    archives fatJar
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '6.9'
}
